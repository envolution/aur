# Maintainer: envolution
# Contributor: Einhard Leichtfu√ü <alguien@respiranto.de>
# Contributor: Tai Chi Minh Ralph Eastwood <tcmreastwood@gmail.com>
# shellcheck shell=bash disable=SC2034,SC2154

pkgname=dict-gcide
pkgver=0.48.5+nmu4
pkgrel=1
epoch=1
pkgdesc="GNU version of CIDE for dictd et al. (debian version tracking)"
arch=('any')
url="https://gcide.gnu.org.ua/"
license=('GPL-3.0-or-later')
optdepends=('dictd: dict client and server')
makedepends=('dictd' 'libmaa') # needs dictzip from dictd to build
install="${pkgname}.install"
source=(
        "https://deb.debian.org/debian/pool/main/d/${pkgname}/${pkgname}_${pkgver}.tar.xz"
      )
sha512sums=('71134c69ece86226233c0693af1d8c776df469bbf3d4969f4a8a883061da2e23eafda2ad395648350ee3fe56f271abd20d7d3fd37c34f0401200317ed1a249be')
validpgpkeys=('325F650C4C2B6AD58807327A3602B07F55D0C732')

build()
{
	cd ${pkgname}
	./configure --prefix=/usr --host=${CHOST}
	make -j1 db # race condition in Makefile with parallel build
}

package()
{
	install -Dm 0644 -t "${pkgdir}/usr/share/dictd/" \
		"${pkgname}"/gcide.{dict.dz,index}
	install -Dm 0644 -t "${pkgdir}/usr/share/doc/dict-gcide/" \
		"${pkgname}"/README
}
# vim:set ts=2 sw=2 et:
