# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Maintainer: László Várady <laszlo.varady93@gmail.com>
# Contributor: envolution
# Contributor: Aleksandar Trifunović <akstrfn at gmail dot com>
# Contributor: Daichi Shinozaki <dsdseg@gmail.com>
# shellcheck shell=bash disable=SC2034,SC2154

pkgname=proxygen
pkgver=2025.08.18.00
pkgrel=1
pkgdesc="A collection of C++ HTTP libraries including an easy to use HTTP server"
arch=(x86_64)
url="https://github.com/facebook/proxygen"
license=(BSD-3-Clause)
depends=(
  boost-libs
  double-conversion
  fizz=$pkgver
  fmt
  folly=$pkgver
  gcc-libs
  gflags
  glibc
  google-glog
  mvfst=$pkgver
  openssl
  wangle=$pkgver
  zlib
  zstd
  c-ares
)
makedepends=(
  boost
  cmake
  gperf
  gtest
  python
)
provides=(
  liblibhttperf2.so
  libproxygen.so
  libproxygencurl.so
  libproxygendeviousbaton.so
  libproxygenhqloggerhelper.so
  libproxygenhttpserver.so
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('2c28aa692544116fd34224310165e344588922be973164b572b9b1b5e9b24f54')

prepare() {
  cd $pkgname-$pkgver
  # Use system CMake config instead of bundled module, incompatible with glog
  # v0.7.0+
  sed -i '/find_package(fmt REQUIRED)/a find_package(Glog CONFIG REQUIRED)' \
    CMakeLists.txt
}

build() {
  cd $pkgname-$pkgver
  cmake -S . -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev \
    -DBUILD_TESTS=ON \
    -DBUILD_SHARED_LIBS=ON \
    -DCARES_STATIC=OFF \
    -DPACKAGE_VERSION=$pkgver
  cmake --build build
}

check() {
  cd $pkgname-$pkgver
  # Skip failing tests - not sure why they fail
  local skipped_tests=(
    ConnectionFilterTest.Test
    GetListenSocket.TestBootstrapWithBinding
    GetListenSocket.TestBootstrapWithNoBinding
    HTTPSessionAcceptorTestNPN.AcceptorConfigCapture
    HTTPSessionAcceptorTestNPN.Npn
    HTTPSessionAcceptorTestNPNJunk.Npn
    HTTPSessionAcceptorTestNPNPlaintext.PlaintextProtocols
    HTTPMessage.TestParseQueryParamsSimple
    SSL.DisallowInsecureOnSecureServer
    SSL.SSLTest
    SSL.SSLTestWithMultiCAs
    SSL.TestAllowInsecureOnSecureServer
    SSL.TestResumptionAfterUpdateFails
    SSL.TestResumptionWithTickets
    SSL.TestResumptionWithTicketsTLS12
    SSL.TestUpdateTLSCredentials
    ScopedServerTest.StartSSLWithInsecure
  )
  local skipped_tests_pattern="${skipped_tests[0]}$(printf "|%s" "${skipped_tests[@]:1}")"
  ctest --test-dir build --output-on-failure -E "$skipped_tests_pattern"
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
# vim:set ts=2 sw=2 et:
